map =
  8 : \backspace
  9 : \tab
  13: \enter
  27: \esc
  32: \space
  37: \left
  38: \up
  39: \right
  40: \down

keychord = (e) ->
  # XXX control characters (eg esc, backspace) don't emit `keypress`
  # `charCode` is 0 except in `keypress`
  # `keyCode` is not unicode
  {which:code} = e
  keys = for <[ alt ctrl meta ]>
    & if e[& + \Key]
  if e.type is \keypress
    key = String.fromCharCode code
  else
    key = map[code]
    if e.shiftKey
      keys.push \shift
  keys.push key

  keys * \+

module.exports = keychord
